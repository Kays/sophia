#!/usr/bin/env perl
#                   _     _       
#   ___  ___  _ __ | |__ (_) __ _ 
#  / __|/ _ \| '_ \| '_ \| |/ _` |
#  \__ \ (_) | |_) | | | | | (_| |
#  |___/\___/| .__/|_| |_|_|\__,_|
#  =========~|_|~=================
#
#       sophia Perl IRC bot

package sophia;
require 5.10.0;
use strict;
use warnings;
use Carp qw(carp croak);
use FindBin qw($Bin);
use Getopt::Long;
use lib "$Bin/../src";
use POE qw(Component::IRC);
use POSIX qw(setsid);
use Sub::Delete;
use feature 'switch';

our %CONFIGURATIONS;
%CONFIGURATIONS = (
    VERSION         => 'sophia-2.0.0-dev',
    MAIN_CONFIG     => "$Bin/../etc/sophia.conf",
    MODULES_CONFIG  => "$Bin/../etc/sophia.modules.conf",
    ACL_DB          => "$Bin/../etc/sophia.acl.db",
    GLOBAL_MODULE   => 'sophia',
    IN_DEBUG_MODE   => 0,
    DO_RESTART      => 0,
);

require Main;

# don't run sophia as root idiot!
trigger_error('sophia', 'Do not run sophia as root!') if  $> == 0;

# due to lack of expertise, sophia currently does not support Windows
trigger_error('sophia', 'Currently, sophia is not supporting Windows.') if  $^O =~ /win/i;

# load configuration values
# ... and if it doesn't exist?
trigger_error('sophia', 'Missing config file.') unless -e $CONFIGURATIONS{MAIN_CONFIG};

my %opts;
GetOptions(
    'debug!' => \$opts{debug},
);

$CONFIGURATIONS{IN_DEBUG_MODE} = 1 if $opts{debug};

require ACL;
require ACLHandler;
require ModuleHandler;

our (%COMMANDS, %MODULES, %TIMERS, %EVENTS, %EVENTSCONF);
my %CMD_ALIASES;

%EVENTSCONF = (
    join            => 'join',
    kick            => 'kick',
    nick            => 'nick',
    notice          => 'notice',
    part            => 'part',
    privmsg         => 'privmsg',
    public          => 'public',
    quit            => 'quit',
);

my %SOPHIA = (
    channels    => {},
    realname    => 'sophia',
    nick        => 'sophia',
    owner_name  => '',
    owner_host  => '',
    password    => '',
    port        => 6667,
    server      => 'chat.freenode.net',
    trigger     => '!',
    usermode    => '+i',
    username    => 'sophia',
    usessl      => 0,
);

# and to load the config ...
&sophia_load_config;

# set the master of sophia
sophia_set_master($SOPHIA{owner_name}, $SOPHIA{owner_host});
&sophia_acl_db_load;

my $pid;
unless ($CONFIGURATIONS{IN_DEBUG_MODE}) {
    $pid = fork();

    unless ($pid == 0) {
        print "sophia forked into the background with pid: $pid\n";
        sophia_log('sophia',"sophia forked into the background with pid: $pid");

        open PIDFILE, "> $Bin/../etc/sophia.pid" or trigger_error('sophia', 'Missing etc directory.');
        print PIDFILE "$pid";
        close PIDFILE;

        exit;
    }
    setsid or trigger_error('sophia', "sophia cannot start a new session: $!");
}
else {
    $pid = $$;
}

unless ($CONFIGURATIONS{IN_DEBUG_MODE}) {
    open STDIN, '/dev/null' or trigger_error('sophia', "sophia cannot read /dev/null: $!\n");
    open STDOUT, '>> /dev/null' or trigger_error('sophia', "sophia cannot write to /dev/null: $!\n");
    open STDERR, '>> /dev/null' or trigger_error('sophia', "sophia cannot write to /dev/null: $!\n");
}

my $sophia = POE::Component::IRC->spawn(
    Nick        => $SOPHIA{nick},
    Username    => $SOPHIA{username},
    Password    => $SOPHIA{password},
    Ircname     => $SOPHIA{realname},
    Server      => $SOPHIA{server},
    Port        => $SOPHIA{port},
    UseSSL      => $SOPHIA{usessl},
) or trigger_error('sophia', "Unable to spawn POE::Component::IRC: $!\n");

POE::Session->create(
    inline_states => {
        _default    => \&_default,
        _start      => \&_start,
        _stop       => \&_stop,
        irc_001     => \&irc_001,
        irc_disconnected    => \&irc_disconnected,
        irc_error   => \&irc_error,
        irc_join    => \&irc_join,
        irc_kick    => \&irc_kick,
        irc_msg     => \&irc_msg,
        irc_nick    => \&irc_nick,
        irc_notice  => \&irc_notice,
        irc_part    => \&irc_part,
        irc_public  => \&irc_public,
        irc_quit    => \&irc_quit,
        irc_shutdown    => \&irc_shutdown,
        sig_int     => \&sig_int,
        load_timers => \&load_timers,
    },
    heap => { sophia => \$sophia },
);

sub _default {
	my ($event, $args) = @_[ARG0 .. $#_];
	my @output = ( "$event: " );

	for my $arg (@$args) {
		if ( ref $arg eq 'ARRAY' ) {
			push( @output, '[' . join(', ', @$arg ) . ']' );
		}
		else {
			push ( @output, "'$arg'" );
		}
	}
	print join ' ', @output, "\n";
	return 0;
}

sub _start {
    my $heap = $_[HEAP];
    $_[KERNEL]->sig( INT => 'sig_int' );

    trigger_error('sophia', "Unable to get the sophia instance from heap (start): $!\n")
        unless $sophia;

    $sophia->yield( register =>  'all' );
    $sophia->yield( connect => { } );

    return;
}

sub _stop {
}

sub irc_001 {
    my $sender = $_[SENDER];
    
    trigger_error('sophia', "Unable to get a sophia instance from heap (001): $!\n")
        unless $sophia;

    # identify to NickServ (if need be)
    $sophia->yield( privmsg => 'NickServ' => sprintf('identify %s %s', $sophia->{nick}, $sophia->{password}) )
        if $sophia->{password};

    # user modes
    $sophia->yield( mode => sprintf('%s %s', $sophia->{nick}, $SOPHIA{usermodes}) )
        if $sophia->{usermodes};

    $sophia->yield( join => $_ ) for keys %{$SOPHIA{channels}};

    # load modules
    sophia_load_modules();

    # hook up timers
    $_[KERNEL]->alarm( 'load_timers' => time() + 1 );
}

sub irc_disconnected {
    $sophia->yield('shutdown');
}

sub irc_error {
    sophia_log('sophia', $_[ARG0]);
}

sub irc_join {
    exec_events($EVENTSCONF{join}, \@_);
}

sub irc_kick {
    exec_events($EVENTSCONF{kick}, \@_);
}

sub irc_msg {
    exec_events_trigger($EVENTSCONF{privmsg}, \@_);
}

sub irc_nick {
    exec_events($EVENTSCONF{nick}, \@_);
}

sub irc_notice {
    exec_events_trigger($EVENTSCONF{notice}, \@_);
}

sub irc_part {
    exec_events($EVENTSCONF{part}, \@_);
}

sub irc_public {
    exec_events($EVENTSCONF{public}, \@_);
    exec_triggers(\@_);
}

sub irc_quit {
    exec_events($EVENTSCONF{quit}, \@_);
}

sub irc_shutdown {
    &sophia_acl_db_save;
    if ($CONFIGURATIONS{DO_RESTART}) {
        if ($CONFIGURATIONS{IN_DEBUG_MODE}) {
            `$Bin/sophia --debug`;
        }
        else {
            `$Bin/sophia`;
        }
    }
    exit;
}

sub sig_int {
    $sophia->yield( quit => 'Shutting down ... ' );
    $_[KERNEL]->sig_handled();
}

sub load_timers {
    sophia_load_timers(\$_[KERNEL]);
    $_[KERNEL]->alarm( 'load_timers' => time() + 60 );
}

POE::Kernel->run();

sub exec_events {
    my ($namespace, $args) = @_;

    for my $m (keys %{$sophia::EVENTS->{$namespace}}) {
        for my $c (keys %{$sophia::EVENTS->{$namespace}{$m}}) {
            eval {
                &{$sophia::EVENTS->{$namespace}{$m}{$c}{init}}($args);
                1;
            } or sophia_log('sophia', '[ERROR] ' . $!) and sophia_log('sophia', '[ERROR] '. $@);
        }
    }
}

sub exec_events_trigger {
    my ($namespace, $args) = @_;
    my @args = @{$args};
    my ($who, $content) = @args[ARG0, ARG2];

    # triggers ?
    return unless $content && $content =~ /^$SOPHIA{trigger}/;

    my $perms = sophia_get_host_perms($who);
    return if $perms & 0x0040;

    $content =~ s/^$SOPHIA{trigger}//;

    my ($module, $command, $trigger, $ac, $x);

    if (index($content, ' ') > -1) {
        $trigger = substr $content, 0, index($content, ' ');
    }
    else {
        $trigger = $content;
    }

    my $target = '';
    $target = substr $args[ARG0], 0, index($args[ARG0], '!') if ($namespace eq $EVENTSCONF{privmsg} || $namespace eq $EVENTSCONF{notice});

    # is this an alias? 
    $trigger = $CMD_ALIASES{$trigger} if defined $CMD_ALIASES{$trigger};

    # global event trigger
    if ($sophia::EVENTS->{$namespace}{$CONFIGURATIONS{GLOBAL_MODULE}}{$trigger}) {
        $ac = $sophia::EVENTS->{$namespace}{$CONFIGURATIONS{GLOBAL_MODULE}}{$trigger}{access};
        if ($ac) {
            return unless $perms & $ac;
        }
        eval {
            $x = &{$sophia::EVENTS->{$namespace}{$CONFIGURATIONS{GLOBAL_MODULE}}{$trigger}{init}}($args, $target);
            1;
        } or sophia_log('sophia', '[ERROR] ' . $!) and sophia_log('sophia', '[ERROR] ' .$@);
    }
    # module:command trigger
    elsif ($trigger =~ m/^([^:]+):(.+)$/) {
        ($module, $command) = ($1, $2);
        if ($sophia::EVENTS->{$namespace}{$module}{$command}) {
            $ac = $sophia::EVENTS->{$namespace}{$module}{$command}{access};
            if ($ac) {
                return unless $perms & $ac;
            }
            eval {
                $x = &{$sophia::EVENTS->{$namespace}{$module}{$command}{init}}($args, $target);
                1;
            } or sophia_log('sophia', '[ERROR] '. $!) and sophia_log('sophia', '[ERROR] ' . $@);
        }
    }
}

sub exec_triggers {
    my $param = $_[0];
    my @args = @{$param};
    my ($who, $where, $content) = @args[ARG0 .. ARG2];

    # triggers ?
    return unless $content && $content =~ /^$SOPHIA{trigger}/;

    my $perms = sophia_get_host_perms($who, $where->[0]);
    return if $perms & 0x0040;   # cheap hack ... 0x0040 = SOPHIA_ACL_BANNED

    $content =~ s/^$SOPHIA{trigger}//;

    my ($module, $command, $trigger, $ac, $x);

    if (index($content, ' ') > -1) {
        $trigger = substr $content, 0, index($content, ' ');
    }
    else {
        $trigger = $content;
    }

    # is this an alias? 
    $trigger = $CMD_ALIASES{$trigger} if defined $CMD_ALIASES{$trigger};

    # sophia command / global trigger?
    if ($sophia::COMMANDS->{$CONFIGURATIONS{GLOBAL_MODULE}}{$trigger}) {
        $ac = $sophia::COMMANDS->{$CONFIGURATIONS{GLOBAL_MODULE}}{$trigger}{access};
        if ($ac) {
            return unless $perms & $ac;
        }
        eval {
            $x = &{$sophia::COMMANDS->{$CONFIGURATIONS{GLOBAL_MODULE}}{$trigger}{init}}($param);
            1;
        } or sophia_log('sophia', '[ERROR] ' . $!) and sophia_log('sophia', '[ERROR] '. $@);
    }
    # module:command trigger
    elsif ($trigger =~ m/^([^:]+):(.+)$/) {
        $module = $1;
        $command = $2;

        if ($sophia::COMMANDS->{$module}{$command}) {
            $ac = $sophia::COMMANDS->{$module}{$command}{access};
            if ($ac) {
                return unless $perms & $ac;
            }
            eval {
                $x = &{$sophia::COMMANDS->{$module}{$command}{init}}($param);
                1;
            } or sophia_log('sophia', '[ERROR] ' . $!) and sophia_log('sophia', '[ERROR] '. $@);
        }
    }
}

sub sophia_load_config {
    my @opts;
    my $len;

    open CONF, $CONFIGURATIONS{MAIN_CONFIG}
        or trigger_error('sophia', "Error opening config file: $!");

    LINE: while (<CONF>) {
        chomp;
        s/^\s+//;
        next LINE if /^#/ || /^\s*$/;  # ignoring comments and lame lines

        @opts = split /\s+/;
        $len = scalar @opts;

        $opts[0] = lc $opts[0];

        given ($opts[0]) {
            when ('alias')  { next LINE if $len != 3; $CMD_ALIASES{$opts[1]} = $opts[2]; }

            default         {
                next LINE if $len != 2;

                if ($opts[0] eq 'channel')          { $SOPHIA{channels}{$opts[1]} = 1; }
                elsif ($opts[0] eq 'port')          {
                    if (index($opts[1], '+') == 0) {
                        $SOPHIA{usessl} = 1;
                        $opts[1] = substr $opts[1], 1;
                    }
                    $SOPHIA{port} = $opts[1];
                }
                elsif ($opts[0] eq 'trigger')       { $SOPHIA{trigger} = sprintf('\%s', $opts[1]); }
                elsif (exists $SOPHIA{$opts[0]})    { $SOPHIA{$opts[0]} = $opts[1]; }
            }
        }
    }
    close CONF;
}

sub sophia_reload_config {
    return 0 unless -e $CONFIGURATIONS{MAIN_CONFIG} && -r $CONFIGURATIONS{MAIN_CONFIG};
    
    # clear all channels
    $SOPHIA{channels}{$_} = 0 for keys %{$SOPHIA{channels}};

    my %OLD_SETTINGS = %SOPHIA;

    &sophia_load_config;

    # if the config changed port, server, ircname, or username, restart sophia
    if ($SOPHIA{port} ne $OLD_SETTINGS{port} || $SOPHIA{server} ne $OLD_SETTINGS{server} || $SOPHIA{realname} ne $OLD_SETTINGS{realname} || $SOPHIA{username} ne $OLD_SETTINGS{username}) {
        $CONFIGURATIONS{DO_RESTART} = 1;
        $sophia->yield(quit => 'Restarting ...');
        return 1;
    }

    # re-set master
    sophia_set_master($SOPHIA{owner_name}, $SOPHIA{owner_host});

    my @part_chans = grep { $SOPHIA{channels}{$_} == 0 } keys %{$SOPHIA{channels}};

    # part all channels not in channel list
    $sophia->yield( part => $_ ) for @part_chans;

    # clear the parted channels from $SOPHIA{channels} list
    delete $SOPHIA{channels}{$_} for @part_chans;

    # change nick?
    if ($SOPHIA{nick} ne $OLD_SETTINGS{nick}) {
        $sophia->yield( nick => $SOPHIA{nick} );

        # identify to NickServ?
        $sophia->yield( privmsg => 'NickServ' => sprintf('identify %s %s', $sophia->{nick}, $SOPHIA{password}) ) if ($SOPHIA{password} && $SOPHIA{password} ne $OLD_SETTINGS{password});
    }

    # update mode?
    $sophia->yield( mode => sprintf('%s %s', $sophia->{nick}, $SOPHIA{usermode}) );

    # join channels
    $sophia->yield( join => $_ ) for keys %{$SOPHIA{channels}};

    return 1;
}

sub sophia_save_config {
    return 0 unless -e $CONFIGURATIONS{MAIN_CONFIG} && -w $CONFIGURATIONS{MAIN_CONFIG};

    open CONF, "> $CONFIGURATIONS{MAIN_CONFIG}";
    
    print CONF '# indicates a comment line', "\n\n";
    print CONF 'nick ', $SOPHIA{nick}, "\n";
    print CONF 'username ', $SOPHIA{username}, "\n";
    print CONF 'realname ', $SOPHIA{realname}, "\n";
    print CONF 'password ', $SOPHIA{password}, "\n";
    print CONF 'usermode ', $SOPHIA{usermode}, "\n\n\n";
    print CONF 'server ', $SOPHIA{server}, "\n";
    print CONF 'port ', ($SOPHIA{usessl} ? '+' : ''), $SOPHIA{port}, "\n\n\n";
    print CONF 'owner_name ', $SOPHIA{owner_name}, "\n";
    print CONF 'owner_host ', $SOPHIA{owner_host}, "\n\n\n";
    print CONF 'trigger ', substr($SOPHIA{trigger}, 1), "\n\n\n";
    
    print CONF '# join the following channels', "\n";
    print CONF 'channel ', $_, "\n" for keys %{$SOPHIA{channels}};

    print CONF "\n\n";
    print CONF '# command aliases', "\n";
    print CONF 'alias ', $_, ' ', $CMD_ALIASES{$_}, "\n" for keys %CMD_ALIASES;

    close CONF;

    return 1;
}

sub sophia_set_config_option {
    my $opts = $_[0];
    my @opts = @{$opts};
    return 0 if scalar(@opts) < 3;

    $opts[1] = lc $opts[1];
    return 0 unless $opts[1] ne 'channel'; # can't add channels

    given ($opts[1]) {
        when ('port') {
            if (index($opts[2], '+') == 0) {
                $SOPHIA{usessl} = 1;
                $opts[2] = substr $opts[2], 1;
            }
            $SOPHIA{port} = $opts[2];
        }
        when ('trigger') { $SOPHIA{trigger} = sprintf('\%s', $opts[2]); }

        default { 
            return 0 unless exists $SOPHIA{$opts[1]};
            $SOPHIA{$opts[1]} = $opts[2];
        }
    }

    return 1;
}

sub sophia_get_aliases {
    return \%CMD_ALIASES;
}

sub sophia_set_alias_option {
    my ($opt1, $opt2) = @_;
    return 0 unless $opt1 && $opt2;

    ($opt1, $opt2) = (lc $opt1, lc $opt2);

    $CMD_ALIASES{$opt1} = $opt2;
    return 1;
}

sub sophia_del_alias_option {
    my $opt = $_[0];
    return 0 unless $opt;

    $opt = lc $opt;
    return 0 unless defined $SOPHIA{$opt};

    delete $SOPHIA{$opt};
    return 1;
}

sub sophia_get_config {
    my %config = %SOPHIA;
    $config{password} = '';
    return \%config;
}
